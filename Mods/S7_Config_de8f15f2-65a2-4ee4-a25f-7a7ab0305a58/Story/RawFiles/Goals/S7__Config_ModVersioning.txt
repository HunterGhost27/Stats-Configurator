Version 1
SubGoalCombiner SGC_AND
INITSECTION

//  ==================================    MOD VERSION (NAME, CURRENT VERSION)    ========================================
//  DB_S7_ModIdentifier("S7_Config", "0.5.2.5");        //  When changing Mod-Version, replace all occurrences. (Ctrl+F2) 
//  =====================================================================================================================

//  =====================================================================================================================
//  DB_S7_ChangeLogs((STRING)_ModName,  (STRING)_Type,                                              (ITEMGUID)_ITEMGUID);
    DB_S7_ChangeLogs("S7_Config",       "Log",               "S7_Config_CHANGELOG_7b4a334e-7613-4ab9-bc21-a724519d6e6e");
    DB_S7_ChangeLogs("S7_Config",       "Book",   "S7_BOOK_StatsConfiguratorHowTo_36f4a82d-bd13-416e-a8a9-409df4773914");
//  =====================================================================================================================

KBSECTION

//  ==================
//  UPDATE MOD VERSION
//  ==================

IF
GameStarted(_, _)
AND
NOT DB_S7_ModIdentifier("S7_Config", "0.5.2.5")                         //  Latest-Version does not exist.
THEN
Proc_S7_Config_Updater();                                               //  Run Update Procedure.
GlobalSetFlag("S7_Config_CHANGELOG");                                   //  Request CHANGELOG.
DB_S7_ModIdentifier("S7_Config", "0.5.2.5");                            //  Add the latest version in the DB.
S7_DebugLog("S7_Config Updated to Version: 0.5.2.5", "[ModVersioning]", "ModID", "S7_Config, 0.5.2.5");

//====================
//REGION CHANGELOG

//  _____________________________________
//  FUNCTION ---    AN ACTUAL LOG
//  *************************************

PROC
Proc_S7_Config_ChangelogRequest()                                    //  Procedure Definition.
AND
DB_IsPlayer(_Character)
AND
DB_S7_ChangeLogs("S7_Config", _, _LogItem)                           //  Fetches LogItems from DB.
AND
ItemTemplateIsInPartyInventory(_Character, _LogItem, 1, 1)           //  Character already has CHANGELOG. (old instances for example).
THEN
ItemTemplateRemoveFromUser(_LogItem, _Character, 1);                 //  Removes old instances of items from player's inventory.

PROC
Proc_S7_Config_ChangelogRequest()                                    //  Procedure Definition.
AND
DB_IsPlayer(_Character)     
AND
DB_S7_ChangeLogs("S7_Config", _ItemType, _LogItem)                   //  Gets the Note and Log from the DB.
AND
NOT ItemTemplateIsInPartyInventory(_Character, _LogItem, 1, 1)       //  Checks if player doesn't already have those items in inventory.
AND
StringConcatenate(_ItemType, " added to player inventory.", _LogPlayerLog)
THEN
ItemTemplateAddTo(_LogItem, _Character, 1);                          //  Adds the Note and Changelog to the player's inventory.
S7_DebugLog(_LogPlayerLog, "[ModVersioning]");


//END_REGION
//====================

//====================
//REGION UPDATER

//  __________________________________________________
//  FUNCTION ---       Clears Mod-Identifier
//  **************************************************

PROC    
Proc_S7_Config_Updater()                             
AND
DB_S7_ModIdentifier("S7_Config", _ModVersion)                           //  Gets all facts from the ModIdentifier.
AND
_ModVersion != "0.5.2.5"                                                //  Filters the latest fact out, if it exists. - it shouldn't though
THEN
NOT DB_S7_ModIdentifier("S7_Config", _ModVersion);                      //  Removes all older facts from DB.

//END_REGION
//====================

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "S7__Config_Initializer"
