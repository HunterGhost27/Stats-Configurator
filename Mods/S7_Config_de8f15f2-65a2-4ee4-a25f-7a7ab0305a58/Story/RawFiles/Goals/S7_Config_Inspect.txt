Version 1
SubGoalCombiner SGC_AND
INITSECTION

//  ====================================================================================================================================
//  |                                                            INSPECT                                                               |
//  ====================================================================================================================================

KBSECTION

//  _______________________________
//      Learn/Unlearn Inspect Skill
//  *******************************

IF
GlobalFlagSet("S7_LearnInspect")                                                        //  Flag set by dialog.
AND
CharacterGetHostCharacter(_Character)                                                   //  Character is the host.
THEN
CharacterAddSkill(_Character, "Target_S7_Config_Inspect", 1);                           //  Add Inspect Skill.
NRD_ModCall("S7_Config", "S7DebugPrint", "Host learned Inspect Spell.", "[ModMenu]");

IF
GlobalFlagCleared("S7_LearnInspect")                                                    //  Flag cleared by dialog.
AND
CharacterGetHostCharacter(_Character)                                                   //  Character is the host.
THEN
CharacterRemoveSkill(_Character, "Target_S7_Config_Inspect");                           //  Remove Inspect Skill.
NRD_ModCall("S7_Config", "S7DebugPrint", "Host unlearned Inspect Spell.", "[ModMenu]");

//  __________________________________
//          Inspect Skill
//  **********************************

IF
CharacterUsedSkillOnTarget(_Character, _Target, "Target_S7_Config_Inspect", _, _)       //  Character cast Inspect on Target.
AND
CharacterGetHostCharacter(_Character)                                                   //  Character is the host.
THEN
Proc_S7_Config_Inspect(_Target);                                                        //  Procedure Call.

PROC
Proc_S7_Config_Inspect((GUIDSTRING)_Target)
AND
ObjectIsCharacter(_Target, 1)                                                           //  Inspect Target is a Character.
THEN
NRD_CharacterIterateSkills((CHARACTERGUID)_Target, "S7_Config_Inspect_TargetSkills");   //  Iterate Over Character's Skills
NRD_IterateStatuses(_Target, "S7_Config_Inspect_TargetStatuses");                       //  Iterate Over Character's Active Statuses

IF
NRD_SkillIteratorEvent("S7_Config_Inspect_TargetSkills", _Target, _SkillID, _, _)       //  Skill-Iterator Event
THEN
Proc_S7_InspectStat(_SkillID);

IF
NRD_StatusIteratorEvent("S7_Config_Inspect_TargetStatuses", _Target, _StatusID, _)      //  Status Iterator Event
THEN
Proc_S7_InspectStat(_StatusID);

PROC
Proc_S7_Config_Inspect((GUIDSTRING)_Target)
AND
ObjectIsItem(_Target, 1)                                                                //  Inspect Target is an item
AND
NRD_ItemGetStatsId((ITEMGUID)_Target, _TargetID)                                        //   Gets StatID for the corresponding item
THEN
Proc_S7_InspectStat(_TargetID);

PROC
Proc_S7_InspectStat((STRING)_StatID)                                                    //  Procedure Definition
AND
NRD_StatExists(_StatID)                                                                 //  Check if StatExists
AND
NRD_StatGetType(_StatID, _StatType)                                                     //  Fetch StatType
AND
StringConcatenate("Inspected (", _StatType, _Str1)
AND
StringConcatenate(_Str1, "): ", _Str2)
AND
StringConcatenate(_Str2, _StatID, _StrMsg)
THEN
NRD_ModCall("S7_Config", "S7DebugPrint", _StrMsg, "[ModMenu]");                         //  Print to Debug-Console

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "S7__Config_Initializer"
