Version 1
SubGoalCombiner SGC_AND
INITSECTION
//  ====================================================================================================================================
//  |                                                           MOD-MENU                                                               |
//  ====================================================================================================================================

//  =============================================
//  DB_S7_Config_ModMenu((STRING)_FlagName)
    DB_S7_Config_ModMenu("S7_StatsConfigurator");
    DB_S7_Config_ModMenu("S7_StatsSynchronize");
//  =============================================

KBSECTION
//=============================
//REGION  START MOD-MENU DIALOG

//  _________________________________
//  Character Used The Stat-Inspector
//  *********************************

IF                                      
CharacterUsedItemTemplate(_Character, "S7_Config_Inspector_c5959819-25e9-4dbc-ae20-0f6283502254", _)     // Character used Inspector.
AND
DB_IsPlayer(_Character)                                        
AND
QRY_SpeakerIsAvailable(_Character)                                          //  Checks whether player can have a dialog.
THEN
Proc_StartDialog(1, "S7_Config_ModMenu", _Character);                //  Starts the Mod-Menu dialog.
NRD_DebugLog("[S7:Config - ModMenu] --- Activated.");

//END_REGION
//=============================

IF
GlobalFlagSet(_Signal)      //  Flag set during ModMenu dialog.
AND
DB_S7_Config_ModMenu(_Signal)   //  Fetch FlagName from DB.
THEN
S7_Config_ModMenuSignal(_Signal);   //  Signal Lua.
GlobalClearFlag(_Signal);           //  Clear Flag.


//  ===========================
//REGION    INSPECT

//  _______________________________
//      Learn/Unlearn Inspect Skill
//  *******************************

IF
GlobalFlagSet("S7_LearnInspect")
AND
CharacterGetHostCharacter(_Character)
THEN
CharacterAddSkill(_Character, "Target_S7_Config_Inspect", 1);

IF
GlobalFlagCleared("S7_LearnInspect")
AND
CharacterGetHostCharacter(_Character)
THEN
CharacterRemoveSkill(_Character, "Target_S7_Config_Inspect");

//  __________________________________
//          Inspect Skill
//  **********************************

IF
CharacterUsedSkillOnTarget(_Character, _Target, "Target_S7_Config_Inspect", _, _)
AND
CharacterGetHostCharacter(_Character)
THEN
Proc_S7_Config_Inspect(_Target);

PROC
Proc_S7_Config_Inspect((GUIDSTRING)_Target)
AND
ObjectIsCharacter(_Target, 1)
THEN
NRD_CharacterIterateSkills((CHARACTERGUID)_Target, "S7_Config_Inspect_TargetSkills");
NRD_IterateStatuses(_Target, "S7_Config_Inspect_TargetStatuses");

IF
NRD_SkillIteratorEvent("S7_Config_Inspect_TargetSkills", _Target, _SkillID, _, _)
AND
NRD_StatGetType(_SkillID, _SkillIDType)
THEN
S7_InspectStats(_SkillID, _SkillIDType);

IF
NRD_StatusIteratorEvent("S7_Config_Inspect_TargetStatuses", _Target, _StatusID, _)
AND
NRD_StatGetType(_StatusID, _StatusIDType)
THEN
S7_InspectStats(_StatusID, _StatusIDType);

PROC
Proc_S7_Config_Inspect((GUIDSTRING)_Target)
AND
ObjectIsItem(_Target, 1)
AND
NRD_ItemGetStatsId((ITEMGUID)_Target, _TargetID)
AND
NRD_StatGetType(_TargetID, _TargetIDType)
THEN
S7_InspectStats(_TargetID, _TargetIDType);


//END_REGION
//  ===========================

EXITSECTION

ENDEXITSECTION
ParentTargetEdge "S7__Config_Initializer"
